apply plugin: 'com.android.application'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props[propName]
    } else {
        return "";
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "nick.arora.todo2015"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        buildConfigField("String", "PARSE_APP_ID", getProps("parseAppId"))
        buildConfigField("String", "PARSE_REST_API_KEY", getProps("parseRestApiKey"))
        buildConfigField("String", "PARSE_END_POINT", getProps("parseEndPoint"))

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.16'
    //compile 'com.google.dagger:dagger:2.0.2'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    //compile 'com.squareup.retrofit:retrofit-converters:1.9.0'
    //compile 'com.squareup.retrofit:retrofit-adapters:1.9.0'

    //compile 'com.squareup.okhttp:okhttp:2.5.0'
    //compile 'com.squareup.picasso:picasso:2.5.2'
    //compile 'com.squareup.sqlbrite:sqlbrite:0.4.1'
    //compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    //compile 'com.github.japgolly.android:svg-android:2.0.6'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.31-beta'
    testCompile 'com.google.truth:truth:0.27'

    androidTestCompile('com.android.support.test:runner:0.4.1') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.4') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude module: 'support-annotations'
        exclude group: 'javax.inject'
    }
    androidTestCompile('com.squareup.assertj:assertj-android:1.1.1') {
        exclude module: 'support-annotations'
    }
}

/* signing application */
def props = new Properties()
props.load(new FileInputStream(rootProject.file("signing.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword
